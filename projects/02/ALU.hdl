// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Zero the x input if zx == 1
    Mux16(a=x, b=false, sel=zx, out=x1);
    // Negate the x input if nx == 1
    Not16(in=x1, out=x2);
    Mux16(a=x1, b=x2, sel=nx, out=x3);
    // Zero the y input if zy == 1
    Mux16(a=y, b=false, sel=zy, out=y1);
    // Negate the y input if ny == 1
    Not16(in=y1, out=y2);
    Mux16(a=y1, b=y2, sel=ny, out=y3);

    // Compute the output based on f
    And16(a=x3, b=y3, out=out1);
    Add16(a=x3, b=y3, out=out2);
    Mux16(a=out1, b=out2, sel=f, out=out3);

    // Compute out and the ng, prep for zr
    Not16(in=out3, out=out4);
    Mux16(a=out3, b=out4, sel=no, out=out, out[15]=ng, out[0..7]=outlow, out[8..15]=outhigh);

    // Compute the zr
    Or8Way(in=outlow, out=o1);
    Or8Way(in=outhigh, out=o2);
    Or(a=o1, b=o2, out=o3);
    Not(in=o3, out=zr);

}